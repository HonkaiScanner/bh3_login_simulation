apply plugin: 'com.android.application'
apply plugin: 'com.huawei.agconnect'
apply plugin: ASMPlugin

android {

    if (!System.getenv("CI")) {
        android.applicationVariants.all { variant ->
            variant.outputs.all { output ->
                def abi = output.getFilter(com.android.build.OutputFile.ABI)
                if (abi == null) abi = "universal"
                outputFileName = "Scanner-${variant.versionName}-${abi}.apk"
            }
        }
    }
    splits {
        abi {
            enable true
            reset()
            include "arm64-v8a", "x86"
            universalApk true
        }
    }


    signingConfigs {
        if (!System.getenv("CI")) {

            debugSign {

                def properties = new Properties()

                def inputStream = project.rootProject.file('local.properties').newDataInputStream()
                storeFile file(System.getenv("KEYSTORE") ?: "keystore.jks")
                properties.load(inputStream)

                storePassword properties.getProperty("sign.pwn")
                keyAlias properties.getProperty("sign.keyAlias")
                keyPassword properties.getProperty("sign.pwn")

            }
        }

        releaseSign {
            storeFile file(System.getenv("KEYSTORE") ?: "keystore.jks")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }
    compileSdkVersion 33
    buildToolsVersion '33.0.1'

    defaultConfig {
        applicationId "com.github.haocen2004.bh3_login_simulation"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 67
        versionName '1.8.0'
//        versionCode Integer.parseInt(System.currentTimeMillis().toString()[7..12])
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        signingConfig signingConfigs.releaseSign

    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            if (System.getenv("CI")) {
                versionNameSuffix System.getenv("NAME_SUFFIX")
            } else {
                versionNameSuffix '-snapshot-23w13c.dev'
            }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix "-dev"
//            applicationIdSuffix '.dev'
            minifyEnabled false
            shrinkResources false

            if (!System.getenv("CI")) {
                signingConfig signingConfigs.debugSign
            }

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
    namespace 'com.github.haocen2004.login_simulation'
}

configurations {
    all*.exclude group: 'com.huawei.hms', module: 'hmscoreinstaller'
    all*.exclude group: 'androidx.appcompat', module: 'appcompat'
}

dependencies {

    implementation fileTree(dir: "libs", include: ["*.jar", "*.so", '*.aar'])
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.navigation:navigation-fragment:2.5.3'
    implementation 'androidx.navigation:navigation-ui:2.5.3'
    implementation 'androidx.viewpager2:viewpager2:1.1.0-beta01'
    implementation 'com.github.jenly1314.WeChatQRCode:opencv:1.2.0'
    implementation 'com.github.jenly1314.WeChatQRCode:opencv-armv7a:1.2.0'
    implementation 'com.github.jenly1314.WeChatQRCode:opencv-armv64:1.2.0'
    implementation 'com.github.jenly1314.WeChatQRCode:opencv-x86:1.2.0'
    implementation 'com.github.jenly1314.WeChatQRCode:opencv-x86_64:1.2.0'
    implementation 'com.github.jenly1314.WeChatQRCode:wechat-qrcode:1.2.0'
    implementation 'com.github.jenly1314.WeChatQRCode:wechat-qrcode-scanning:1.2.0'
    implementation 'com.github.jenly1314.MLKit:mlkit-camera-core:1.2.0'
    implementation project(':mi_sdk')
//    implementation project(':360_sdk')
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.tencent.bugly:crashreport:4.1.9'
    implementation 'cn.leancloud:storage-android:8.2.17'
    implementation 'cn.leancloud:realtime-android:8.2.17'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation "androidx.room:room-runtime:2.5.0"
//    implementation 'com.google.android.material:material:1.7.0'
    implementation 'dev.rikka.rikkax.material:material:2.5.1'
    implementation 'dev.rikka.rikkax.material:material-preference:2.0.0'
    implementation 'com.github.bumptech.glide:glide:4.14.2'
    implementation 'com.github.getActivity:ToastUtils:9.1'
    implementation 'com.github.getActivity:XToast:8.9'
    implementation 'com.geetest.sensebot:sensebot:4.3.9.1'
    implementation 'com.drakeet.about:about:2.5.2'
    implementation 'com.drakeet.multitype:multitype:4.3.0'
//    implementation 'com.huawei.agconnect:agconnect-core:1.7.1.300'
    implementation 'com.huawei.hms:hwid:6.8.0.300'
    implementation 'com.huawei.hms:game:6.8.0.300'
    implementation 'org.json:json:20220924'
    implementation 'com.google.android.material:material:1.4.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.14.2'
    annotationProcessor 'androidx.room:room-compiler:2.5.0'
//    testImplementation 'junit:junit:4.13.2'
//    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
//    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'

}
